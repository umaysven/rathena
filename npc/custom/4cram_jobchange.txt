//===== eAthena Script ======================================= 
//= 4CrAM Job Changer
//===== By: ================================================== 
//= Rytech
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= 4CrAM - Expanded Tier
//===== Description: ========================================= 
//= Allows changing into trait Era Jobs.
//===== Additional Comments: ================================= 
//= 1.0 Release Version.
//= 1.01 Updated required job level to 70.
//= 2.0 Added trait era expanded jobs.
//============================================================
prontera,158,184,4	script	Special Job Changer	485,{

	mes "[Special Job Changer]";
	mes "Hey there. Are you ready to change into your final job?";
	next;
	menu "Yes",Xready,"No",Xnotready;

Xready:
	set @reqbaselv,200;// Required base level to change to trait era jobs.
	if (Class == Job_Summoner)
		set @reqjoblv,60;// Required job level for Summoners.
	else
		set @reqjoblv,70;// Required job level for all over jobs.

	if (checkcart() || checkfalcon() || checkriding() || checkdragon() || checkwug() || checkmadogear())
		goto Xremove;

	// Only trans versions of 3rd jobs are allowed to change to 4th jobs.
	// kRO has some special method for allowing regular 3rd jobs to change to 4th but im not sure what it is.
	// Maybe its a convert from regular 3rd to trans 3rd. Has to be since all 4th jobs get access to trans skills.
	if ((Class >= Job_Rune_Knight_T && Class <= Job_Guillotine_Cross_T) || (Class >= Job_Royal_Guard_T && Class <= Job_Shadow_Chaser_T) || 
		Class == Job_Super_Novice_E || (Class >= Job_Kagerou && Class <= Job_Summoner) || Class == Job_Star_Emperor || Class == Job_Soul_Reaper)
		goto Xtraitjob;
	else
		goto Xcantchange;

Xtraitjob:
	if (BaseLevel >= @reqbaselv && JobLevel >= @reqjoblv)
	{
		if (Class == Job_Rune_Knight_T)
			jobchange Job_Dragon_Knight;
		else if (Class == Job_Warlock_T)
			jobchange Job_Arch_Mage;
		else if (Class == Job_Ranger_T)
			jobchange Job_Windhawk;
		else if (Class == Job_Arch_Bishop_T)
			jobchange Job_Cardinal;
		else if (Class == Job_Mechanic_T)
			jobchange Job_Meister;
		else if (Class == Job_Guillotine_Cross_T)
			jobchange Job_Shadow_Cross;
		else if (Class == Job_Royal_Guard_T)
			jobchange Job_Imperial_Guard;
		else if (Class == Job_Sorcerer_T)
			jobchange Job_Elemental_Master;
		else if (Class == Job_Minstrel_T)
			jobchange Job_Troubadour;
		else if (Class == Job_Wanderer_T)
			jobchange Job_Trouvere;
		else if (Class == Job_Sura_T)
			jobchange Job_Inquisitor;
		else if (Class == Job_Genetic_T)
			jobchange Job_Biolo;
		else if (Class == Job_Shadow_Chaser_T)
			jobchange Job_Abyss_Chaser;
		else if (Class == Job_Super_Novice_E)
			jobchange Job_Hyper_Novice;
		else if (Class == Job_Star_Emperor)
			jobchange Job_Sky_Emperor;
		else if (Class == Job_Soul_Reaper)
			jobchange Job_Soul_Ascetic;
		else if (Class == Job_Rebellion)
			jobchange Job_Night_Watch;
		else if (Class == Job_Kagerou)
			jobchange Job_Shinkiro;
		else if (Class == Job_Oboro)
			jobchange Job_Shiranui;
		else if (Class == Job_Summoner)
			jobchange Job_Spirit_Handler;

		goto Xcomplete;
	}
	else
		goto Xlowlevel;

Xremove:
	mes "[Special Job Changer]";
	mes "You must remove your mount, falcon, warg, and cart before you can change.";
	close;

Xnotready:
	mes "[Special Job Changer]";
	mes "Ok then. Come back when you feel your ready.";
	close;


Xcantchange:
	mes "[Special Job Changer]";
	mes "Sorry but there's no jobs you can change to through me with your current job.";
	close;

Xcomplete:
	mes "[Special Job Changer]";
	mes "Congrats. You have reached your true final job.";
	close;

Xlowlevel:
	mes "[Special Job Changer]";
	mes "Sorry but your base level must be "+@reqbaselv+" and your job level must be "+@reqjoblv+".";
	close;
}
